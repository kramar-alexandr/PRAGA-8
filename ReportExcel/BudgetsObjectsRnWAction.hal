remote procedure BudgetsObjectsRn(record RcVc, var string);
remote function boolean OTCodeMatch(string,string);
remote function boolean isProject(string);
remote function boolean GetPartFileToArea(string,var longint,var area);

global
procedure RunBudgetObjectsRn()
begin
	string 255 filename,tstr,fnserv;
	area tmparea;
	record RcVc RepSpec;
	integer wn;
	boolean testf,EOF;
	string 10 tmpDate;
  record ObjVc Objr;
  record PRVc PRr;
  LongInt arealen;
	
  wn = curwindow;
	deselectwindow(wn,true);
	getwindowrecord(wn,RepSpec);
	testf = true;
	if (blank(RepSpec.f3)) then begin
		MessageBox(0,"Input Year!");
		testf = false;
	end else begin
		if len(RepSpec.f3)!=4 then  begin
			MessageBox(0,"Incorrect Year!");
			testf = false;
		end;
	end;
	if ((RepSpec.flags[1]==0) and (RepSpec.flags[2]==0) and (RepSpec.flags[3]==0) and (RepSpec.flags[4]==0) and testf) then begin
		MessageBox(0,"Select at least one Quartal!");
		testf = false;
	end;
	if ((((RepSpec.flags[1]==1) and (RepSpec.flags[2]==0) and ((RepSpec.flags[3]==1) or (RepSpec.flags[4]==1))) or (((RepSpec.flags[1]==1) or (RepSpec.flags[2]==1)) and (RepSpec.flags[3]==0) and (RepSpec.flags[4]==1))) and testf) then begin
		MessageBox(0,"Incorrect selection of Quartals!");
		testf = false;
	end;
	if (nonblank(RepSpec.f2) and testf) then begin
		if !isProject(RepSpec.f2) then begin
			testf = false;
			MessageBox(0,"Incorrect Project!");
		end else begin
      PRr.Code = RepSpec.f2;
      readfirstmain(PRr,1,true);
      if ((GetYear(PRr.StartDate) & ""!=RepSpec.f3) and (GetYear(PRr.EndDate) & ""!=RepSpec.f3)) then begin
        testf = false;
        MessageBox(0,"Project does not match year!");
      end;
    end;
	end;
  if (nonblank(RepSpec.f1) and testf) then begin
    Objr.Code = RepSpec.f1;
    if (readfirstmain(Objr,1,true)) then begin
      if OTCodeMatch(RepSpec.f1,"EXP") then begin
        if (RepSpec.f3=="2016" and right(RepSpec.f1,3)!="_16") or (RepSpec.f3!="2016" and right(RepSpec.f1,3)=="_16") then begin
          testf = false;
          MessageBox(0,"Object with type " & chr(34) & "EXP" & chr(34) & " does not match year!");
        end;
      end;
      if OTCodeMatch(RepSpec.f1,"PRJ") then begin
        testf = false;
        MessageBox(0,"Input object with type " & chr(34) & "PRJ" & chr(34) & " in another field!");
      end;
    end else begin
      testf = false;
      MessageBox(0,"Object does not exist!");
    end;
	end;
	if testf then begin
		tmpDate = "";
		if (RepSpec.flags[4]==1) then begin
			tmpDate = "1/10/";
		end;
		if (RepSpec.flags[3]==1) then begin
			tmpDate = "1/7/";
		end;
		if (RepSpec.flags[2]==1) then begin
			tmpDate = "1/4/";
		end;
		if (RepSpec.flags[1]==1) then begin
			tmpDate = "1/1/";
		end;
		tmpDate = tmpDate & RepSpec.f3;
		RepSpec.sStartDate = StringToDate(tmpDate);
		
		tmpDate = "";
		if (RepSpec.flags[1]==1) then begin
			tmpDate = "31/3/";
		end;
		if (RepSpec.flags[2]==1) then begin
			tmpDate = "30/6/";
		end;
		if (RepSpec.flags[3]==1) then begin
			tmpDate = "30/9/";
		end;
		if (RepSpec.flags[4]==1) then begin
			tmpDate = "31/12/";
		end;
		tmpDate = tmpDate & RepSpec.f3;
		RepSpec.sEndDate = StringToDate(tmpDate);
		tstr = "Budget Report";
		if nonblank(RepSpec.f2) then begin
			tstr = tstr & " for Project #" & RepSpec.f2;
		end;
		if nonblank(RepSpec.f1) then begin
			tstr = tstr & " Object-" & RepSpec.f1;
		end;
		tstr = tstr & " filled in on " & DateToString(CurrentDate,"DD MM YYYY");
		filename = SaveFileDialog(tstr,"","xlsx");
		if ((filename!=tstr) and nonblank(filename)) then begin
			BudgetsObjectsRn(RepSpec,fnserv);
			if(fileexists(filename))then begin
				delete_file(filename);
			end;
			arealen = 102400;
      while (!EOF) begin
        EOF = GetPartFileToArea(fnserv,arealen,tmparea);
        writeareatofile(tmparea,filename,1);
      end;
      CloseWindow(wn);
			Messagebox(0,"Done!");
		end;
	end;
	return;
end;